#!/usr/bin/env sh
#yt_title_clean - clean up music title from youtube

exec sed '
s/\([Oo]fficial\|OFFICIAL\)[^][(){}.-]*//;
s/\([Ee]xtended\|EXTENDED\)[^][(){}.-]*//;
s/\([Ww]ith [Ll]yrics\|WITH LYRICS\)//;
s/\(• \|[Ww]ith \|\)[Ll]yrics//;
s/\([Oo]riginal [Mm]ix\|ORIGINAL MIX\)//;
s/\([Mm]usic [Vv]ideo\|MUSIC VIDEO\)//;
s/\([Aa]udio [Oo]nly\|[Aa]udio\|AUDIO\)//;
s/\([Vv]ideo\|VIDEO\)//;
s/\([Ll]ive\|LIVE\)//;
s/(\([Ss]ingle\|[Ss]tudio\) [Vv]ersion)//;
s/\([Vv]isualizer\|VISUALIZER\)//;
s/([^()]*[Vv]ideo)//;
s/[Ll]ong [Ee]dit$//;
s/\.mp4//;
s/HQ//;
s/HD Video//;
s/\(1080p\|720p\)//;
s/\( \|\)([12][0-9]\{3\})//;
s/ [][(){}. |-]*$//;
s/\( \|\)\(()\|\[\]\|{}\)//g;
s/\(- \|[([{]\)\([Ff]ull [Ee][Pp]\|FULL EP\|EP\)\(\|[])}]\)/[EP]/;
s/\(- \|[([{]\)\([Ff]ull [Aa]lbum\|FULL ALBUM\|ALBUM\)\(\|[])}]\)/[Album]/;
s/–/-/g;
s/—/-/g;
s/- -/-/g;
s/\([^ ]\)- /\1 - /;
s/\(.*\) - "\([^"]*\)"$/\1 - \2/;
'"s/\(.*\) - '\([^']*\)'\$/\1 - \2/;"'
/[^-]*-[^-]*$/s/\(.*[^ ]\) *- */\1 - /;
/[^-:]*:[^-:]*$/s/\(.*[^ ]\) *: */\1 - /;
/[^-~]*~[^-~]*$/s/\(.*[^ ]\) *~ */\1 - /;
/[^-"]*"[^-"]*"$/s/\([^"]*\) "\([^"]*\)"/\1 - \2/;
'"/[^-']*'[^-']*'\$/s/\([^']*\) '\([^']*\)'/\1 - \2/;"'
/[^-●]*●●[^-●]*$/s/\(.*[^ ]\) *●● */\1 - /;
/[^-|]*|[^-|]*$/s/\(.*[^ ]\) *| */\1 - /;
'
